//===== rAthena Script =======================================
//= Kafra Functions
//===== By: ==================================================
//= rAthena Dev Team
//= Darlskies, Darkchild, Syrus22, Lupus, kobra_k88 (2.0)
//= L0ne_W0lf
//===== Current Version: =====================================
//= 6.8
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= These functions handle save, storage, cart rental, teleport,
//= and Free Teleport/Warp/Rent Cart options for all Kafra NPCs.
//=
//= Kafra's will need a lot of work. The Welcome Message argument
//= may become obsolete, as most kafras have a slightly differing
//= Welcome message.
//===== Additional Comments: =================================
//= 1.1 Now using functions v2.1 Added Cart Rent for Classes: Whitesmith, Professor.
//=     Replaced checkoption(x) into checkcart(0) [Lupus] v2.1b Added Fix Kafra Pass Func [Kobra_k88]
//= 2.2 Final fix of the Kafra Pass Exploit! [Lupus] a -Izlude[4] fix
//= 2.2a Minor changes to function calls.  Using agruments. Added Guild options. [kobra_k88]
//= 2.2b This version uses arrays for the teleport option. Rearranged next statements to make menu transitions smoother. [kobra_k88]
//= 2.3 Removed SAVE from Niflheim. [Lupus]
//= 2.3 removed "fix" by HawkMoon RTFM and check supernovice.txt . There's a SPECIAL Kafra which gives CARTS to SN for a special proce. [Lupus]
//= 2.4 Added Baby Class Support (Baby Novice check) Removed annoying storage feature where u had to close dialog window to be able to use your storage [Lupus]
//= 2.5 Added Luoyang official Kafra, fixes some Kafras, not letting you to Save your position [Lupus]
//= 2.6 Reverted Dungeons Kafras (they should offer only Storage). Added temp Ayothaya Kafra
//= 2.7 Added correct Ayothaya, Luoyang & Amatsu Kafras. [Lupus]
//= 2.8 Fixed Amatsu Storage problems [Lupus] 2.9 Fixed spelling mistakes. [Nexon]
//= 3.0 Added special "not working teleport menu" for Einbroch Kafras [Lupus]
//= 3.1 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 3.2 Fixed an exploit [Lupus]
//= 4.0 Added Kafra storage password protection. [Lupus]
//=     to block Kafra Password, read comments at 350 line
//= 5.0 Fixed the close2;....close; end; lines. Who let them slip away? =/ [erKURITA]
//=     Also, the kafra upon warp was giving back the zeny. Removed. [erKURITA]
//= 5.1 Optimized a little, added no tele/save arg's [Evera]
//= 5.1a Temporary? Added F_ClearGarbage to clear unused/outdated variables [Lupus]
//= 5.2 By default commented out custom Guilds Kafra's "Guild Storage". [Lupus]
//= 5.3 uncommented Guild Storage. Confirmed kRO feature. [Lupus]
//= 5.4 added -Guide option (Kafra shows you nearest Guide) Work in progress.
//=     Need all coords for all guides.. and somehow pass their coords to kafra.
//=     Removed universal Kafra Pass, added 3 new Kafra Tickets [Lupus]
//= 5.4b temporary moved "-Guide" from the 1st menu punct [Lupus]
//= 5.5 Added proper Niflheim welcome message.	[L0ne_W0lf]
//=     Cleaned up the menus a bit. Got rid of the proceeding "-" prefix.
//= 5.5b Missed a preceeding hyphen, which was screwing up teleporting. [L0ne_W0lf]
//= 5.6 Further dialog updates, added  dynamic costs for cart and storage use. [L0ne_W0lf]
//=     Updated some functions to handle Guild Kafras the way they should.
//= 5.6a Corrected a few wrong names in the teleport function. [L0ne_W0lf]
//= 5.7 Correced end message no longer displaying, and the duplicate names [L0ne_W0lf]
//=     Thanks to Barron-Monster for pointing out the issues.
//= 5.8 Fixed another double name being shown when "saving" [L0ne_W0lf]
//= 5.9 Fixed the Kafra Welcome message for guilds. Thanks Barron-Monster. [L0ne_W0lf]
//= 6.0 Closed Kafra Password exploit in Cool Event Corp. Storages until we got a client/packet based password support.
//=     Added 2 args into F_SetKafCode to fit it in Cool Evnt Co. NPC [Lupus]
//= 6.1 Added menu for Turbo Track Kafra Staff. [L0ne_W0lf]
//= 6.2 Updated/Fixed warp cords. [Kisuka]
//= 6.3 #kafra_code is now stored as is. [brianluau]
//= 6.4 Removed unofficial Guide function. [Daegaladh]
//= 6.5 Optimized. [Joseph]
//= 6.6 Added Warp Points / View Points Function. [Joseph]
//= 6.7 Added Rune Knight Kafra. [Euphy]
//= 6.8 Updated some functions and added VIP features. [Euphy]
//============================================================

//=== Main Function ========================================================
//=   arg(0): Used to determine which welcome message to show.
//=   arg(1): Used to determine which menu to display.
//=   arg(2): Used to determine if the info menu is shown in F_KafInfo.
//=   arg(3): Cost to use Storage
//=   arg(4): Cost to Rent a Pushcart
//==========================================================================
function	script	F_Kafra	{
	callfunc "F_ClearGarbage"; // Clear outdated, unused variables
	.@welcome = getarg(0);
	.@menu_num = getarg(1);

	// Display Kafra Welcome Message
	switch(.@welcome) {
	default:// Default message (obsolete)
		mes "[Funcionária Kafra]";
		mes "Bem-vindo à";
		mes "Corporação Kafra.";
		mes "Os serviços Kafra";
		mes "estão sempre ao seu lado.";
		mes "Como posso ajudá-lo?";
		break;
	case 1:// Niflheim specific message
		mes "[Funcionária Kafra]";
		mes "^666666B-beeeeem-v-vinda";
		mes "à C-cooorpoorraaação";
		mes "K-kaaafrrrra...^000000";
		break;
	case 2:// Guild Castle Kafra message
		mes "[Funcionária Kafra]";
		mes "Bem-vindo. Membro da ^ff0000" + getguildname( getcharid(2) ) + "^000000.";
		mes "A Corporação Kafra estará com você onde quer que vá.";
		break;
	case 3:// Amatsu specific message (obsolete)
		mes "[Funcionária Kafra]";
		mes "Então, você veio de terras distantes para estudar nossa cultura, ou está apenas fazendo turismo?";
		mes "De qualquer forma, por que não ficar um pouco mais?";
		mes "O ar está eternamente carregado com o";
		mes "aroma agradável de flores silvestres.";
		break;
	case 4:// Luoyang and Ayothaya specific message (obsolete)
		mes "[Funcionária Kafra]";
		mes "Com nossas várias";
		mes "localizações de serviço Kafra, você nunca";
		mes "está longe de casa.";
		break;
	case 5:// NPC has it's own welcome message. (Display nothing)
		break;
	}
	next;
	if (.@welcome == 2)// Guild Kafra Menu override (free Teleport, free Storage, Free Cart)
		setarray .@K_Menu0$[0], "Usar Armazém", "Usar Armazém da Guilda", "Alugar um Carrinho", "Usar Serviço de Teleporte", "Cancelar";
	else {
		switch(.@menu_num) {
			// Save and Storage only
			case 1:	setarray .@K_Menu0$[0],"Salvar","Usar Armazém","Cancelar"; break;
			// Storage only
			case 2:	setarray .@K_Menu0$[0],"Usar Armazém","Cancelar"; break;
			// No Teleport (Common)
			case 3: setarray .@K_Menu0$[0],"Salvar","Usar Armazém","Alugar um Carrinho","Verificar Outras Informações","Cancelar"; break;
			// Case 4 is Einbroch no tele message.
			// No save, or teleport. (Common)
			case 5:	setarray .@K_Menu0$[0],"Usar Armazém","Alugar um Carrinho","Verificar Outras Informações","Cancelar"; break;
			// Storage and Check Other Information only.
			case 6:	setarray .@K_Menu0$[0],"Usar Armazém","Verificar Outras Informações","Cancelar"; break;
			// Save, Storage, and Pushcart only (Kafra Warehouse)
			case 7:	setarray .@K_Menu0$[0],"Salvar","Usar Armazém","Alugar um Carrinho","Cancelar"; break;
			// Save, Storage, Other Check information. (Turbo track)
			case 8: setarray .@K_Menu0$[0],"Salvar","Usar Armazém","Verificar Outras Informações","Cancelar"; break;
			// No Save (Rune Knight)
			case 9: setarray .@K_Menu0$[0],"Usar Armazém","Alugar um Carrinho","Usar Serviço de Teleporte","Verificar Outras Informações","Cancelar"; break;
			// Storage, Save, and Pushcart (Dewata, reorder of case 7)
			case 10: setarray .@K_Menu0$[0],"Usar Armazém","Salvar","Alugar um Carrinho","Cancelar"; break;
			// Default message (obsolete)
			default: setarray .@K_Menu0$[0],"Salvar","Usar Armazém","Usar Serviço de Teleporte","Alugar um Carrinho","Verificar Outras Informações","Cancelar"; break;
		}
	}
	.@menu$ = implode(.@K_Menu0$,":");
	while (true) {
		.@j = select(.@menu$) - 1;
		if (.@K_Menu0$[.@j] == "Salvar")
			mes "[Funcionária Kafra]";
			mes "Seu local de renascimento foi salvo aqui.";
			set .@map$, strcharinfo(3);
			getmapxy(.@x, .@y, 0);
			savepoint .@map$, .@x, .@y;
			close;
			return;
		else if (.@K_Menu0$[.@j] == "Usar Armazém") {
			if (.@welcome == 2)// Do not charge for Guild Storage
				callfunc "F_KafStor",2,0,0;
			else
				callfunc "F_KafStor",0,getarg(3),.@welcome;
			next;
		}
		else if (.@K_Menu0$[.@j] == "Usar Serviço de Teleporte") {
			if (.@menu_num != 4)// Display Einbroch "No Teleport Service" notice.
				callfunc "F_KafTele",.@welcome;
			else {
				mes "[Funcionária Kafra]";
				mes "Devido ao ^FF0000Acordo de Transporte";
				mes "Limitado^000000, a Corporação";
				mes "Kafra não pode fornecer";
				mes "Serviços de Teleporte na";
				mes "República de Schwarzwald.";
				next;
				mes "[Funcionária Kafra]";
				mes "Pedimos que, por favor,";
				mes "use o Serviço de Aeronave";
				mes "em vez disso. Obrigada pela";
				mes "compreensão e cooperação.";
			}
			next;
		}
		else if (.@K_Menu0$[.@j] == "Alugar um Carrinho") {
			if (callfunc("F_KafCart",.@welcome,getarg(4)) == 1)
				next;
		}
		else if (.@K_Menu0$[.@j] == "Verificar Outras Informações") {
			callfunc "F_KafInfo",getarg(2);
			next;
		}
		else if (.@K_Menu0$[.@j] == "Cancelar") {
			callfunc "F_KafEnd",.@welcome,0;
			end;
		}
		else if (.@K_Menu0$[.@j] == "Usar Armazém da Guilda") {
			callfunc "F_KafStor",1,0;
			next;
		}
	}
}

//===================================================
// Storage Function
//===================================================
function	script	F_KafStor	{
	.@type = getarg(0);
	.@fee = getarg(1);

	// Unable to access Guild Storage (Busy)
	if (.@type == 1) {
		if (guildopenstorage()) {
			mes "[Funcionária Kafra]";
			mes "Desculpe, mas outro membro da guilda está usando o armazém da guilda";
			mes "agora. Por favor, espere até que essa pessoa termine.";
			close3;
		}
		cutin "", 255;
		close;
	}
	// Unable to access Normal Storage (Insufficient Basic Skills)
	if (!callfunc("F_CanOpenStorage")) {
		mes "[Funcionária Kafra]";
		// Niflheim Specific Message
		if (getarg(2) == 1) {
			mes "^666666D-d-deeesculpe,";
			mes "v-você é u-u-uum";
			mes "Nooviiiço... P-precisa";
			mes "H-habilidade B-básica n-nível 6...^000000";
			return;
		}
		mes "Sinto muito, mas você";
		mes "precisa da Habilidade";
		mes "Básica de Aprendiz Nível 6 para";
		mes "usar o Serviço de Armazém.";
		return;
	}
	// Accessing Normal Storage (Skipped if accessing Storage from Guild castle)
	if (.@type != 2) {
		// Consume "Free Ticket for Kafra Storage" if available.
		if (countitem(7059))
			delitem 7059,1;
		else {
			if (Zeny < .@fee) {
				mes "[Funcionária Kafra]";
				// Niflheim Specific Message
				if (getarg(2) == 1) {
					percentheal -50,-50;
					mes "^666666Zeeeenys...";
					mes "M-mais z-zenys...!";
					mes "P-preciso de 150... zenys...";
					mes "Argh! P-pegando s-sangue~!^000000";
					return;
				}
				// Standard Message
				mes "Desculpe, mas você não";
				mes "tem zenys suficientes para usar";
				mes "o Serviço de Armazém. Nossa";
				mes "taxa de acesso ao Armazém é de " + .@fee + " zenys.";
				return;
			}
			Zeny -= .@fee;
			RESRVPTS += (.@fee/5);
		}
	}

	// Surpress the "End" message if in guild castle.
	if (.@type != 2) {
		// Niflheim specific Message
		if (getarg(2) == 1) {
			percentheal 0,-10;
			mes "[Funcionária Kafra]";
			for ( .@i = 0; .@i < 5; .@i++ )
				mes "^666666Obrigada.. por... usar...^000000";
		}
		// Normal message
		else {
			mes "[Funcionária Kafra]";
			mes "Aqui, deixe-me abrir";
			mes "seu Armazém para você.";
			mes "Obrigada por usar";
			mes "o Serviço Kafra.";
		}
	}
	callfunc("F_CheckKafCode");	//check your storage password, if set
	close2;
	openstorage;
	cutin "", 255;
	end;
}

//===================================================
// Teleport Function
//===================================================
function	script	F_KafTele	{
	mes "[Funcionária Kafra]";
	mes "Por favor, escolha";
	mes "seu destino.";
	next;
	.@j = select( implode(@wrpC$,":") ) - 1;
	if (@wrpC$[.@j] == "Cancelar")
		return;
	// Consume "Free Ticket for Kafra Transportation" if available.
	// Do not consume if in Guild castle
	if (countitem(7060) > 0 && getarg(0) != 2)
		delitem 7060,1;
	else {
		if (Zeny < @wrpP[.@j]) {
			mes "[Funcionária Kafra]";
			mes "Desculpe, mas você não tem";
			mes "zenys suficientes para o Serviço";
			mes "de Teleporte. A taxa para teleportar";
			mes "para " + @wrpD$[.@j] + " é de " + @wrpP[.@j] + " zenys.";
			close3;
		}
		Zeny -= @wrpP[.@j];
		RESRVPTS += (@wrpP[.@j]/16);
	}
	cutin "", 255;
	if (@wrpD$[.@j] == "Al De Baran") warp "aldebaran",168,112;
	else if (@wrpD$[.@j] == "Alberta") warp "alberta", 117, 56;
	else if (@wrpD$[.@j] == "Comodo") warp "comodo", 209, 143;
	else if (@wrpD$[.@j] == "Comodo Pharos Beacon") warp "cmd_fild07", 127, 134;
	else if (@wrpD$[.@j] == "Geffen") warp "geffen", 120, 39;
	else if (@wrpD$[.@j] == "Izlude") {
		if (checkre(0) == true)
			warp "izlude", 128, 98;
		else
			warp "izlude", 91, 105;
	}
	else if (@wrpD$[.@j] == "Juno") warp "yuno", 158, 125;
	else if (@wrpD$[.@j] == "Mjolnir Dead Pit") warp "mjolnir_02", 99, 351;
	else if (@wrpD$[.@j] == "Morocc") warp "morocc", 156, 46;
	else if (@wrpD$[.@j] == "Orc Dungeon") warp "gef_fild10", 52, 326;
	else if (@wrpD$[.@j] == "Payon") warp "payon", 161, 58;
	else if (@wrpD$[.@j] == "Prontera") warp "prontera", 116, 72;
	else if (@wrpD$[.@j] == "Umbala") warp "umbala", 100, 154;
	end;
}


//===================================================
// Cart Function
//===================================================
function	script	F_KafCart	{
	// Ensure that the class wanting to rent a pushcart is a merchant
	if (BaseClass != Job_Merchant) {
		mes "[Funcionária Kafra]";
		mes "Desculpe, mas o";
		mes "serviço de aluguel de Carrinho";
		if (checkre(0) == true)
			mes "está disponível apenas para classes Mercador.";
		else {
			mes "está disponível apenas para Mercadores,";
			mes "Ferreiros, Mestres Ferreiros,";
			mes "Alquimistas e Bioquímicos.";
		}
		return 1;
	}
	// Make sure the invoking character does not have a cart already
	else if (checkcart() == true) {
		mes "[Funcionária Kafra]";
		mes "Você já tem";
		mes "um Carrinho equipado.";
		mes "Infelizmente, não podemos";
		mes "alugar mais de um para";
		mes "cada cliente por vez.";
		return 1;
	}
	// Check if the player has the Pushcart skill
	else if (getskilllv("MC_PUSHCART") == 0) {
		mes "[Funcionária Kafra]";
		mes "Você só pode alugar um carrinho após aprender a habilidade \"Carrinho\".";
		return 1;
	}
	// Consume "Free Ticket for the Cart Service" if available.
	// Do not consume if in Guild castle.
	if (countitem(7061) > 0 && getarg(0) != 2)
		delitem 7061,1;
	else {
		.@rental_fee = getarg(1);
		mes "[Funcionária Kafra]";
		mes "A taxa de aluguel";
		mes "do Carrinho é de " + .@rental_fee + " zenys. Você";
		mes "gostaria de alugar um Carrinho?";
		next;
		if (select("Alugar um Carrinho.:Cancelar") == 2)
			return 0;
		if (Zeny < .@rental_fee) {
			mes "[Funcionária Kafra]";
			mes "Desculpe, mas você";
			mes "não tem zenys";
			mes "suficientes para pagar a taxa";
			mes "de aluguel do Carrinho de " + .@rental_fee + " zenys.";
			return 1;
		}
		Zeny -= .@rental_fee;
		RESRVPTS += 48;
	}
	setcart;
	return 1;
}

//===================================================
// Special Reserve Points Function
//===================================================
function	script	F_KafInfo	{
// Uncomment next line to block Kafra Storage Protection
//	.@block = 1;
	if (getarg(0) == 2)// Port Malaya, Rune Knight
		setarray .@menu$[0], "Verificar Pontos de Reserva Especial.", "", "", "Cancelar";
	else if (.@block)
		setarray .@menu$[0], "Verificar Pontos de Reserva Especial.", "", "Localizações das Funcionárias Kafra", "Cancelar";
	else
		setarray .@menu$[0], "Verificar Pontos de Reserva Especial.", "Serviço de Senha do Armazém", "Localizações das Funcionárias Kafra", "Cancelar";
	.@menu_list$ = implode(.@menu$,":");
	while (true) {
		.@j = select(.@menu_list$) - 1;
		switch(.@j) {
		case 0:// Check Special Reserve Points.
			mes "[Funcionária Kafra]";
			mes "Deixe-me ver...";
			mes "" + strcharinfo(0) + "...";
			mes "Ah, você tem um total de";
			mes "" + RESRVPTS + " Pontos de Reserva Especial.";
			next;
			mes "[Funcionária Kafra]";
			mes "Você pode trocar seus";
			mes "Pontos de Reserva Especial por";
			mes "recompensas no Escritório Principal da Kafra em Al De Baran. Por favor, use nossos";
			mes "serviços convenientes para ver os benefícios do nosso programa de recompensas.";
			if (getarg(0) == 1)
				return;
			next;
			break;
		case 1:// Storage Password Service
			callfunc("F_SetKafCode","[Funcionária Kafra]","Serviços Kafra");
		case 2:// Kafra Employee Locations
			for ( .@i = 0; .@i <= 3; .@i++ )
				viewpoint 1,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
			next;
			for ( .@i = 0; .@i <= 3; .@i++ )
				viewpoint 2,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
			break;
		default:
			return;
		}
	}
}

// === End Function ==================================================
//  arg(0): used to determine what message to display.
//  arg(1): used to determine if save message is displayed.
//  arg(2): used to display the name of the area you're saving in.
//===================================================================
function	script	F_KafEnd	{
	.@type = getarg(0);
	mes "[Funcionária Kafra]";
	if (getarg(1) == true) {// Save
		mes "Seu Ponto de Renascimento";
		mes "foi salvo aqui";
		mes "" + getarg(2) + ".";
		mes "Obrigada por usar";
		mes "os Serviços Kafra.";
	} else if (.@type == 0 || .@type == 5) {// Generic End
		mes "Nós, aqui na Corporação Kafra,";
		mes "estamos sempre nos esforçando para fornecer-lhe os melhores serviços. Esperamos que atendamos às suas necessidades de aventura e padrões de excelência.";
	} else if (.@type == 1) {// Niflheim End
		percentheal 0,-25;
		mes "^666666Kaffffra n-nunca";
		mes "moooooorre. Ao...";
		mes "Ao s-seu laaaaado~^000000";
	} else if (.@type == 2) {// RockRidge Save
		mes "Salvo.";
		mes "Obrigada por seu patrocínio.";
	}
	close3;
}

//===================================================
// Check Storage Password Function
//===================================================
function	script	F_CheckKafCode	{
	if (!#kafra_code)
		return;
	mes "Digite sua senha de armazém:";
	input .@code_;
	if (.@code_ != #kafra_code) {
		dispbottom "Senha de armazém incorreta.";
		close3;
	}
	@kafcode_try = 0;
	return;
}

// === Set / Change / Clear Storage Password Function ===
// getarg(0): NPC Name
// getarg(1): Company Name
//=======================================================
function	script	F_SetKafCode	{
	.@npc_name$ = getarg(0);
	.@comp_name$ = getarg(1);

	mes .@npc_name$;
	if (#kafra_code == 0) {
		mes "" + .@comp_name$ + " orgulhosamente apresenta um novo serviço:";
		mes "Proteção adicional de armazém com senha.";
		next;
		if (select( "Definir nova senha -> 5000z", "Cancelar" ) == 1)
			callsub( S_SET, .@npc_name$, .@comp_name$ );
		close3;
	}
	mes "Seu armazém está protegido com uma senha. O que você deseja fazer agora?";
	next;
	switch( select( "Alterar senha antiga -> 5000z", "Remover senha do armazém -> 1000z", "Cancelar" ) ) {
	case 1:
		mes .@npc_name$;
		mes "Primeiro, por favor, digite sua ^0000FFsenha antiga^000000.";
		.@code = callfunc("F_EntKafCode");
		if (!.@code || .@code != #kafra_code) {
			mes "Senha incorreta. Você não pode definir uma nova senha.";
			emotion ET_SCRATCH;
			break;
		}
		next;
		callsub( S_SET, .@npc_name$, .@comp_name$ );
		break;
	case 2:
		mes .@npc_name$;
		mes "Por favor, digite sua senha antes de removê-la.";
		.@code = callfunc("F_EntKafCode");
		if (!.@code) {
			mes "A senha não foi removida.";
			emotion ET_SCRATCH;
			break;
		}
		next;
		mes .@npc_name$;
		if (Zeny < 1000) {
			mes "Você não tem zenys suficientes.";
			emotion ET_MONEY;
			break;
		}
		Zeny -= 1000;
		if (.@code == #kafra_code) {
			#kafra_code = 0;
			mes "Você removeu com sucesso sua senha de armazém.";
			mes "Obrigada por usar " + .@comp_name$ + ".";
			emotion ET_THANKS;
		} else {
			mes "Senha incorreta. Não devolveremos seus 1000z.";
			mes "Por favor, na próxima vez digite a senha correta.";
			emotion ET_SORRY;
		}
		break;
	case 3:
		break;
	}
	close3;

S_SET:
	.@npc_name$ = getarg(0);
	.@comp_name$ = getarg(1);

	mes .@npc_name$;
	mes "Agora digite sua ^FF0000nova senha^000000 para proteger seu armazém de ladrões.";
	.@code = callfunc("F_EntKafCode");
	if (!.@code) {
		mes "A senha não foi alterada.";
		emotion ET_SCRATCH;
		return;
	}
	next;
	mes .@npc_name$;
	if (Zeny < 5000) {
		mes "Você não tem zenys suficientes.";
		emotion ET_MONEY;
		return;
	}
	Zeny -= 5000;
	#kafra_code = .@code;
	mes "Você protegeu seu armazém com uma senha secreta.";
	mes "Obrigada por usar " + .@comp_name$ + ".";
	emotion ET_THANKS;
	return;
}

//===================================================
// Basic Password Validation Function
//===================================================
function	script	F_EntKafCode	{
	mes "Digite um número entre 1000~10000000:";
	@kafcode_try += 1;
	if (@kafcode_try > 10) {
		@kafcode_try = 0;
		logmes "Tentou quebrar a senha do armazém.";
		return 0;
	}
	if (input(.@code_) == 1) {
		mes "Você não pode usar uma senha tão grande.";
		return 0;
	}
	if (.@code_ < 1000) {
		mes "Você não deve usar uma senha tão curta.";
		return 0;
	}
	return .@code_;
}

//===================================================
// Warp Points / View Points Function
//===================================================
function	script	F_KafSet	{
	deletearray @wrpC$[0], getarraysize(@wrpC$);
	deletearray @wrpD$[0], getarraysize(@wrpD$);
	deletearray @wrpP$[0], getarraysize(@wrpP$);
	deletearray @viewpX[0], getarraysize(@viewpX);
	deletearray @viewpY[0], getarraysize(@viewpY);

	.@map$ = strnpcinfo(4);
	if (.@map$ == "prontera") {
		setarray @wrpD$[0], "Izlude", "Geffen", "Payon", "Morocc", "Orc Dungeon", "Alberta";
		setarray @wrpP[0], 600, 1200, 1200, 1200, 1700, 1800;
		setarray @viewpX[0], 151, 29, 282, 152;
		setarray @viewpY[0], 29, 207, 200, 326;
	} else if (.@map$ == "alberta") {
		setarray @wrpP[0], 1200, 1800, 1800;
		setarray @wrpD$[0], "Payon", "Morocc", "Prontera";
		setarray @viewpX[0], 28, 113, 0, 0;
		setarray @viewpY[0], 229, 60, 0, 0;
	} else if (.@map$ == "aldebaran") {
		setarray @wrpP[0], 1200, 1200, 1800, 1700;
		setarray @wrpD$[0], "Geffen", "Juno" , "Izlude", "Mjolnir Dead Pit";
	} else if (.@map$ == "comodo" ) {
		setarray @wrpD$[0], "Morocc", "Comodo Pharos Beacon", "Umbala";
		setarray @wrpP[0], 1800, 1200, 1800;
	} else if (.@map$ == "cmd_fild07") {
		setarray @wrpD$[0], "Comodo", "Morocc";
		setarray @wrpP[0], 1200, 1200;
	} else if (.@map$ == "geffen") {
		setarray @wrpD$[0], "Prontera", "Al De Baran", "Orc Dungeon", "Mjolnir Dead Pit";
		setarray @wrpP[0], 1200, 1200, 1700, 1700;
		setarray @viewpX[0], 120, 203;
		setarray @viewpY[0], 62, 123;
	} else if (.@map$ == "izlude") {
		setarray @wrpD$[0], "Geffen", "Payon", "Morocc", "Al De Baran";
		setarray @wrpP[0], 1200, 1200, 1200, 1800;
	} else if (.@map$ == "morocc") {
		setarray @wrpD$[0], "Prontera", "Payon", "Alberta", "Comodo", "Comodo Pharos Beacon";
		setarray @wrpP[0], 1200, 1200, 1800, 1800, 1200;
		setarray @viewpX[0], 156, 163, 28, 292;
		setarray @viewpY[0], 97, 260, 167, 211;
	} else if (.@map$ == "umbala") {
		setarray @wrpD$[0], "Comodo";
		setarray @wrpP[0], 1800;
	} else if (.@map$ == "payon") {
		setarray @wrpD$[0], "Prontera", "Alberta", "Morocc";
		setarray @wrpP[0], 1200, 1200, 1200;
	} else if (.@map$ == "yuno") {
		setarray @wrpD$[0], "Al De Baran";
		setarray @wrpP[0], 1200;
		setarray @viewpX[0], 328, 278, 153, 0;
		setarray @viewpY[0], 108, 221, 187, 0;
	} else if (.@map$ == "job3_rune01") {
		setarray @wrpD$[0], "Izlude", "Geffen", "Payon", "Morocc", "Alberta";
		setarray @wrpP[0], 600, 1200, 1200, 1200, 1800;
	}

	.@warp_size = getarraysize(@wrpD$);

	// VIP pricing varies across the regional servers; this is most practical.
	if (VIP_SCRIPT && !vip_status(VIP_STATUS_ACTIVE)) {
		for ( .@i = 0; .@i < .@warp_size; .@i++ )
			@wrpP[.@i] *= 2;
	}

	for ( .@i = 0; .@i < .@warp_size; .@i++ )
		@wrpC$[.@i] = @wrpD$[.@i] + " -> " + @wrpP[.@i] + " z";
	@wrpC$[ .@warp_size ] = "Cancelar";
	return;
}
